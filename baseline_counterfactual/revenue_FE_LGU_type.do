* load the data generated by revenue_FE_generate.do

label define lgulab 1 "Regions" 2 "Land counties" 3 "City counties" 4 "Municipalities"
label values LGU lgulab

collapse (sum) Y yhat, by(tq yearq_id LGU)

* Contribution to overall % deviation (so stacks sum to the total %)
gen double pct =  (Y - yhat) / yhat
label var pct "Contribution to total % deviation"

sort LGU tq

* nice x-axis labels
label define yearqlab 1 "19Q1" 2 "19Q2" 3 "19Q3" 4 "19Q4" ///
    5 "20Q1" 6 "20Q2" 7 "20Q3" 8 "20Q4" ///
    9 "21Q1" 10 "21Q2" 11 "21Q3" 12 "21Q4" ///
    13 "22Q1" 14 "22Q2" 15 "22Q3" 16 "22Q4" ///
    17 "23Q1" 18 "23Q2" 19 "23Q3" 20 "23Q4" ///
    21 "24Q1" 22 "24Q2" 23 "24Q3" 24 "24Q4"
    
label values yearq_id yearqlab
    
*-------------------------------*
* 4) Plot the four lines
*-------------------------------*
twoway ///
    (line pct yearq_id if LGU==1, lcolor(navy)           lpattern(solid)         lwidth(medthick)) ///
    (line pct yearq_id if LGU==2, lcolor(forest_green)   lpattern(dash)          lwidth(medthick)) ///
    (line pct yearq_id if LGU==3, lcolor(maroon)         lpattern(dot)           lwidth(medthick)) ///
    (line pct yearq_id if LGU==4, lcolor(orange)         lpattern(shortdash_dot) lwidth(medthick)), ///
    xlabel(1(1)24, valuelabel angle(45) nogrid) /// 
    ylabel(, nogrid)                           /// 
    ytitle("Deviation from baseline") ///
    xtitle("") ///
    legend(order(1 "Regions" 2 "Land counties" 3 "City counties" 4 "Municipalities") ///
           rows(1) position(6)) ///
    graphregion(color(white)) ///
    plotregion(style(none)) ///
    xline(13, lcolor(black) lpattern(solid) lwidth(thin))
